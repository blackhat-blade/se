#!perl
use 5.014;
use warnings;

use Data::Dumper;
use File::Slurp;
use Getopt::Long 2.42 qw/HelpMessage :config auto_help auto_version/;
use Cwd qw(abs_path);

our $VERSION = 0.4;

our @libdir  = ('./lib', '../lib');
our $debug   = 0;
my $infile   = ''; 
my $outfile  = '';
my %libs;
my $curfile = '';
my %pos;
my $cscerrors  = '';
my $debugBuild = '';
my %libLoc;

sub error
{
	my $msg = shift;
	
	if ($cscerrors)
	{
		say STDERR "$curfile($.,0):", " error CS666: ", $msg;
	}
	else
	{
		say STDERR "Error at: ", $curfile, " line ", $., " : ", $msg;
	}
	exit 1;
}

sub debug
{
	
	my $msg = shift;
	
	return unless $debug;
	say STDERR "debug: ", $curfile, $.  ? " line $.: " : '' ,, $msg;
}

sub parse
{
	my $line = shift;

	if ($line =~ /^#use\s+(\w(?:[\w.]+?\w)*)\s*$/)
	{
		debug("found $1");
		
		if (exists $libs{$1})
		{
			debug ("lib $1 already assembled");
		}
		else
		{
			debug("adding $1 to lib list");
			$libs{$1} = 0;
			$pos{$1}  = [$curfile, $.]; #tracks location for better debugging missing libs.
		}
	}
	else
	{
		error "invalid use statement: $line";
	}
}

sub find 
{
	my $lib = shift;
	my $subpath;

	debug "searching $lib";	
	$subpath = ($lib =~ s!\.!/!r) . ".cs";

	foreach (@libdir)
	{
		my $try = join '/', $_, $subpath;  
		debug "trying $try";

		if (-e $try)
		{
			debug "found $try";
			$libLoc{$lib} = abs_path($try);
			return $try;
		}
	}
	($curfile, $.) = @{$pos{$lib}}; #retrieves location of first request
	error "can't find sourcefile for $lib in @libdir";
}

sub unresolved
{
	grep { !$libs{$_} } sort keys %libs;
}

sub assemble
{
	my @unresolved = unresolved;
	my $round      = shift || 0;
	
	++$round;
	
	debug "assembler round: $round";
	debug "assembling libs: @unresolved";
	
	foreach my $lib (@unresolved)
	{
		my $filename;
		debug "assembling $lib";
		
		$filename = find $lib;
		scan($filename, $lib);
		
	}

	if (unresolved)
	{
		debug "some new depencies showed up, making another round";
		assemble($round);
	}
}

sub scan
{
	my $in      = shift;
	my $modname = shift || '@IN';


	my $buf;
	
	debug "scanning $modname";

	$curfile = $in;
	open FILE, '<', $in or die "can't open file: $!";
	
	while (my $line = <FILE>)
	{
		if ($line =~ /^#use\s+/)
		{
			parse $line;
			$line = '//'. $line;
		}

		$buf .= $line;		
	}

	close FILE or die "can't close file: $!";
	$curfile = '';
	
	debug "adding lib content for $modname";
	$libs{$modname} = $buf;  
}

sub build
{
	my $outfile = shift;
	my $out;

	debug "writing output to $outfile";

	$out  = delete $libs{'@IN'};
	$out .= "\n\n//end main code\n//everything below has been added by build tool\n";
	$out .= join "\n", map { ($debugBuild ? qq!#line 1 "! . $libLoc{$_}  . qq!"\n!: '') .  "//code for: $_\n" . $libs{$_} . "\n//end $_\n"} sort keys %libs; 

	write_file $outfile, $out or die "couldn't write file ($outfile): $!";

}

sub run
{
	scan $infile;
	assemble;
	build $outfile;
	exit 0;
}


GetOptions('infile=s' => \$infile, 'outfile=s' => \$outfile, verbose => \$debug, 'libdirs=s' => \@libdir, cscerrors => \$cscerrors, debug => \$debugBuild);

unless ($infile)
{
	$infile = shift;
	
	unless ($infile)
	{
		HelpMessage();
	}
}

if ($infile eq '-')
{
	$infile = \*STDIN;
	unless ($outfile)
	{
		$outfile = \*STDOUT;
	}
}

if ($outfile eq '-')
{
	$outfile = \*STDOUT;
}

if ($outfile eq '-CB')
{
	debug ("output to clipboard requested, checking for Win32::Clipboard");
	eval 'require Win32::Clipboard';
	error "Output to clipboard requested, but require Win32::Clipboard failed: $@" if $@;
	
	no warnings 'redefine'; # don't try this at home boys.
	*write_file = sub {Win32::Clipboard::Set($_[1])};

}

unless ($outfile)
{
	$outfile = "$infile.out";
}

run($infile, $outfile);

=head1 NAME

build - an unsophiscated tool to glue together csharp sources for space engineer scripts  

=head1 SYNOPSIS

  perl build INFILE
  perl build --infile INFILE [--outfile OUTFILE] [--verbose] [--libdirs DIR1 --libdirs DIR2 ]
	  
  If INFILE is - STDIN will be used.
  If OUTFILE is - STDOUT will be used.
  If INFILE is - and OUTFILE ist not specified STDOUT will be used.
  If INFILE is not - and OUTFILE is not specified INFILE.out will be used
  If OUTFILE is -CB  output is written to the system clipboard

  Libdirs default to './lib' and '../lib'
  	

=head1 DESCRIPTION

build scans INFILE for '#use Bla' lines. Each matching line is prepended with '//', so the comiler won't panic, and the requested lib will be remembered.

Every lib requested in INFILE will be searched for in LIBDIRS (default to './lib' and '../lib'), with filename LIB.cs . If found the lib will be searched for '#use' statements too, and the source code will be inserted after the last line of INFILEs source. If the lib can't be found in any LIBDIR build aborts with an error. 

Once all libs have been processed the resulting code will be written to OUTFILE.  
  	
Every lib will only be added once, no matter how many times a matching use statement is encountered, this holds true even if the lib is requested by multiple other libs.

=head2 Libs and Filenames

Libnames may only consist of word characters [0-9A-Za-z_] and dots, and must begin and end in a word character. A sample lib named LIBNAME will be searched in files named LIBNAME.cs in all libdirs (the first one found is used). Libs with dots in their name are treated a bit differently, the dots are replaced with slashes, so the lib will be searched in subdirectories of the libdirs, so one can organize libs neatly.

=head3 Examples

  #use Foo     => LIBDIR/Foo.cs
  #use Foo.Bar => LIBDIR/Foo/Bar.cs

=head3 Libdirs

build has two builtin libdirs, './lib' and '../lib' . Additional libdirs can be supplied with --libdir . Libdirs are searched in the order they are given, the builtin first.

=head2 Troubleshooting

If build bails out with any error, or misbehaves elsewise the --verbose flag might help. It enables debugging output. 

=head1 CAVEATS

Currently there is no way to specify additional libdirs beside the --libdir commandline option, and no way to remove './lib' and '../lib' from search path, beside editing the source code. 
 
=head1 AUTHOR

blade 

=head1 LICENSE

This code isn't intended for public use, and not licensed in any way, to nobody. Until it is, use at your own risk, technical, legal or otherwise.

